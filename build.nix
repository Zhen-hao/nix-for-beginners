let
  # pkgs = import <nixpkgs> {};
  rev = "f6bfb371cba2b5a02f200c2747c1fe2c72bd782f";
  # hash generated by doing nix-prefetch-url --unpack https://github.com/NixOS/nixpkgs-channels/archive/{rev}.tar.gz
  hash = "0y3hlbyvznrpr1d2vxj2511hkjg733wdnxfaib3fgy9i9jr8ivzn";

  pkgs = import (builtins.fetchTarball
  {
    name = "nixpkgs-tarball-${rev}";
    url = "https://github.com/NixOS/nixpkgs-channels/archive/${rev}.tar.gz";
    sha256 = hash;
  }) {};

  R-with-my-packages = with pkgs; rWrapper.override { 
      packages = with rPackages; 
        let
        slidify = buildRPackage {
          name = "slidify";
          src = pkgs.fetchFromGitHub {
            owner = "ramnathv";
            repo = "slidify";
            rev = "1dd41a3023b20efb35d601a7d3fb248930bf076d";
            sha256 = "0wsn7dw0qnhvqpvcf57hhyxm7v0rmps1qsn83r3zqvgdkc90107j"; #"0000000000000000000000000000000000000000000000000000";
          };
          propagatedBuildInputs = [ markdown  knitr stringr yaml whisker ];
        };
        slidifyLibraries = buildRPackage {
          name = "slidifyLibraries";
          src = pkgs.fetchFromGitHub {
            owner = "ramnathv";
            repo = "slidifyLibraries";
            rev = "dbd065f4843be1377d1aab5f35c9406c60922aa8";
            sha256 = "1wwxfw25b4m4yw0j6wz7vz9y3pxqymla3ql4mzpfc1g0igfa1v6r";
          };
        };
        in
        [ 
            knitr
            rmarkdown
            slidify
            slidifyLibraries
        ]; 
    };

in

  pkgs.stdenv.mkDerivation {

    name = "my-slides";

    src = builtins.filterSource (
          path: type: 
            baseNameOf path != "shell.nix"
            && baseNameOf path != "slides.nix"
            && baseNameOf path != "build.nix"
            && baseNameOf path != "index.md"
            && baseNameOf path != "index.html"
          ) ./.;

    buildInputs = with pkgs; [
      R-with-my-packages
    ];

    buildPhase = ''
        mkdir -p "$out"
        cp -r $src/* $out
    '';

    installPhase = ''
      cd $out
      Rscript -e 'library(slidify); slidify("index.Rmd")'
    '';

  }
